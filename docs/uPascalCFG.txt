SystemGoal -> Program mp_eof.
Program -> ProgramHeading mp_scolon Block mp_period.
ProgramHeading -> mp_program ProgramIdentifier.
Block -> VariableDeclarationPart ProcedureAndFunctionDeclarationPart StatementPart.
VariableDeclarationPart -> mp_var VariableDeclaration mp_scolon VariableDeclarationTail.
VariableDeclarationTail -> VariableDeclaration mp_scolon VariableDeclarationTail
| .
VariableDeclaration -> IdentifierList mp_colon Type.
Type -> mp_integer.
ProcedureAndFunctionDeclarationPart -> ProcedureDeclaration ProcedureAndFunctionDeclarationPart
| FunctionDeclaration ProcedureAndFunctionDeclarationPart
| .
ProcedureDeclaration -> ProcedureHeading mp_scolon Block mp_scolon.
FunctionDeclaration -> FunctionHeading mp_scolon Block mp_scolon.
ProcedureHeading -> mp_procedure ProcedureIdentifier OptionalFormalParameterList.
FunctionHeading -> mp_function FunctionIdentifier OptionalFormalParameterList mp_colon Type.
OptionalFormalParameterList -> mp_lparen FormalParameterSection FormalParameterSectionTail mp_rparen
| .
FormalParameterSectionTail -> mp_scolon FormalParameterSection FormalParameterSectionTail
| .
FormalParameterSection -> ValueParameterSection
| VariableParameterSection.
ValueParameterSection -> IdentifierList mp_colon Type.
VariableParameterSection -> mp_var IdentifierList mp_colon Type.
StatementPart -> CompoundStatement.
CompoundStatement -> mp_begin StatementSequence mp_end.
StatementSequence -> Statement StatementTail.
StatementTail -> mp_scolon Statement StatementTail
| .
Statement -> EmptyStatement
| CompoundStatement
| ReadStatement
| WriteStatement
| AssignmentStatement
| IfStatement
| WhileStatement
| RepeatStatement
| ForStatement
| ProcedureStatement.
EmptyStatement -> .
ReadStatement -> mp_read mp_lparen ReadParameter ReadParameterTail mp_rparen.
ReadParameterTail -> mp_comma ReadParameter ReadParameterTail
| .
ReadParameter -> VariableIdentifier.
WriteStatement -> mp_write mp_lparen WriteParameter WriteParameterTail mp_rparen.
WriteParameterTail -> mp_comma WriteParameter WriteParameterTail
| .
WriteParameter -> OrdinalExpression.
AssignmentStatement -> VariableIdentifier mp_assign Expression
| FunctionIdentifier mp_assign Expression.
IfStatement -> mp_if BooleanExpression mp_then Statement OptionalElsePart.
OptionalElsePart -> mp_else Statement
| .
RepeatStatement -> mp_repeat StatementSequence mp_until BooleanExpression.
WhileStatement -> mp_while BooleanExpression mp_do Statement.
ForStatement -> mp_for ControlVariable mp_assign InitialValue StepValue FinalValue mp_do Statement.
ControlVariable -> VariableIdentifier.
InitialValue -> OrdinalExpression.
StepValue -> mp_to
| mp_downto.
FinalValue -> OrdinalExpression.
ProcedureStatement -> ProcedureIdentifier OptionalActualParameterList.
OptionalActualParameterList -> mp_lparen ActualParameter ActualParameterTail mp_rparen
| .
ActualParameterTail -> mp_comma ActualParameter ActualParameterTail
| .
ActualParameter -> OrdinalExpression.
Expression -> SimpleExpression OptionalRelationalPart.
OptionalRelationalPart -> RelationalOperator SimpleExpression
| .
RelationalOperator -> mp_equal
| mp_lthan
| mp_gthan
| mp_lequal
| mp_gequal
| mp_nequal.
SimpleExpression -> OptionalSign Term TermTail.
TermTail -> AddingOperator Term TermTail
| .
OptionalSign -> mp_plus
| mp_minus
| .
AddingOperator -> mp_plus
| mp_minus
| mp_or.
Term -> Factor FactorTail.
FactorTail -> MultiplyingOperator Factor FactorTail
| .
MultiplyingOperator -> mp_times
| mp_div
| mp_mod
| mp_and.
Factor -> mp_integer_lit
| VariableIdentifier
| mp_not Factor
| mp_lparen Expression mp_rparen
| FunctionIdentifier OptionalActualParameterList.
ProgramIdentifier -> mp_identifier.
VariableIdentifier -> mp_identifier.
ProcedureIdentifier -> mp_identifier.
FunctionIdentifier -> mp_identifier.
BooleanExpression -> Expression.
OrdinalExpression -> Expression.
IdentifierList -> mp_identifier IdentifierTail.
IdentifierTail -> mp_comma mp_identifier IdentifierTail
| .
VariableDeclarationPart -> .
Type -> mp_float
| mp_string
| mp_boolean.
WriteStatement -> mp_writeln mp_lparen WriteParameter WriteParameterTail mp_rparen.

Factor -> mp_float_lit
| mp_string_lit
| mp_true
| mp_false.